// TODO: Update this to kotlin after updating the distribution plugin extension
plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id 'com.mux.gradle.android.mux-android-distribution'
}

android {
  namespace 'com.mux.player'
  compileSdk 35

  defaultConfig {
    minSdk 21
    //noinspection EditedTargetSdkVersion
    targetSdk 35

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"

    multiDexEnabled true
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

muxDistribution {
  devVersion versionFromCommitHash('dev-')
  releaseVersion versionFromTag()
  artifactIds just("android")
  groupIds just("com.mux.player")
  publicReleaseIf releaseIfCmdFlag("publicRelease")

  // TODO: This is kinda clumsy, packageJavadocs should be a function not a property probably
  packageJavadocs = releaseOnTag().call()
  publishIf { it.containsIgnoreCase("release") }
  artifactoryConfig {
    contextUrl = "https://muxinc.jfrog.io/artifactory/"
    releaseRepoKey = 'default-maven-release-local'
    devRepoKey = 'default-maven-local'
  }

  dokkaConfig {
    moduleName = "Mux Player SDK for Android"
    footer = "(c) " + new Date().format("yyyy") + " Mux, Inc. Have questions or need help?" +
            " Contact support@mux.com"
  }

  pom {
    description "The Mux Player SDK for Android. It's just media3 with some helpful tools"
    inceptionYear = "2022"
    url = "https://github.com/muxinc/mux-stats-sdk-media3"
    organization {
      name = "Mux, Inc"
      url = "https://www.mux.com"
    }
    developers {
      developer {
        email = "support@mux.com"
        name = "The player and sdks team @mux"
        organization = "Mux, inc"
      }
    }
  }
}

dependencies {

  def media3Version = "1.6.1"
  def muxDataVariant = "at_1_6"
  def muxDataVersion = "dev-fix-abstract-method-error-1ee5cdc"
  api "androidx.media3:media3-common:${media3Version}"
  api "androidx.media3:media3-exoplayer:${media3Version}"
  api "androidx.media3:media3-ui:${media3Version}"
  api "androidx.media3:media3-exoplayer-hls:${media3Version}"
  api "androidx.media3:media3-exoplayer-ima:${media3Version}"
  api "androidx.media3:media3-cast:${media3Version}"

  api("com.mux.stats.sdk.muxstats:data-media3-$muxDataVariant:$muxDataVersion")

  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1"

  testImplementation 'junit:junit:4.13.2'
  testImplementation 'androidx.test.ext:junit:1.1.5'
  testImplementation "io.mockk:mockk:1.12.3"
  testImplementation 'org.robolectric:robolectric:4.10.3'

  androidTestImplementation 'androidx.test.ext:junit:1.1.5'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}